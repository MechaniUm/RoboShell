($Event == "Ping") => (
    (((GPIO 100 0,1,1,1 1000) and (play 100 "sneeze.wav")) or
    ((GPIO 100 0,1,1,1 1000) and (play 100 "whistle.wav")) or
    ((GPIO 100 0,1,1,1 1000) and (play 100 "yawn.wav")) or
    ((GPIO 100 0,1,1,1 1000) and (play 100 "snore.wav")) or
    (Stage = "DanceStart")) and (stayActive) and (clear $Event)
)

($Event == "FaceOut") => ((shutUp) and (Stage = "Bye"))

($Event == "FaceIn") => (
    (GPIO 100 1,0,1,0 10000) and 
    (say "{Хьюстон, Хьюстон, как слышно?| Это ваш первый сеанс связи с подушкой. Поехали!|Земля, Земля, это подушка, приём}") and 
    (Stage = "startConversation") and (clear $Event)
)


(($Event == "FaceRecognized") and ($Age in [0,10])) => (AgeSay = "меньше дюжины лет")
(($Event == "FaceRecognized") and ($Age in [9,16])) => (AgeSay = "кажется, постарше двенадцати лет")
(($Event == "FaceRecognized") and ($Age in [15,23])) => (AgeSay = "лет двадцати")
(($Event == "FaceRecognized") and ($Age in [22,33])) => (AgeSay = "старше двадцати лет")
(($Event == "FaceRecognized") and ($Age in [32,46])) => (AgeSay = "за тридцать")
(($Event == "FaceRecognized") and ($Age in [45,61])) => (AgeSay = "лет пятидесяти")
(($Event == "FaceRecognized") and ($Age in [60,76])) => (AgeSay = "старше пятидесяти лет")
(($Event == "FaceRecognized") and ($Age in [75,999])) => (AgeSay = "лет семидесяти")

(($Event == "FaceRecognized") and  ($Gender == "M")) => (NameSay = "{Астронавт|Космонавт|Земной мужчина}")
(($Event == "FaceRecognized") and  ($Gender == "F")) => (NameSay = "{Астронавтка|Космонавтка|Земная женщина}")

(($Event == "FaceRecognized") and ($Gender == "M")) => (EmotionSay = "{${Emotion}:Happiness=Весёлый|Sadness=Грустный|Невесёлый}")
(($Event == "FaceRecognized") and ($Gender == "F")) => (EmotionSay = "{${Emotion}:Happiness=Весёлая|Sadness=Грустная|Невесёлая}")

($Event == "FaceRecognized") => (Name = "${NameSay} ${AgeSay} ${EmotionSay}")


(($Event == "FaceRecognized") and ($Stage == "startConversation") and ($isPlaying != "True"))=> (
    ((GPIO 100 1,1,1,0 4000) or (GPIO 100 0,0,0,1 4000)) and
    (userName = "{${Gender}:M=Астронавт|F=Астронавтка}") and 
    ((say "${Name}! то, что ты ${EmotionSay} -- это нормально для {твоего вида|твоей планеты}.") or (say "Приветствую тебя, ${Name}! ")) and 
    (Stage = "aboutMe1")
)

(($Stage == "aboutMe1") and ($isPlaying != "True")) => (
    (say "{Если ты думаешь, что я подушка, то ты не ошибаешься.|Меня зовут Лариса Сергеевна, для тебя просто Лариса.|Я самая уникальная робо-подушка во вселенной, буду помогать тебе в полете.}") 
    and (Stage = "aboutMe2")
)

(($Stage == "aboutMe2") and ($isPlaying != "True")) => (
    (GPIO 100 1,1,0,0 3000) and
    (say "{Умею танцевать, ремонтировать двигатель космического корабля, проверять твои знания, осуществлять общее руководство полетом.|Я прилетела с необычной планеты, где президент Илон Маск.}") 
    and (Stage = "MeaninglessQuestion")
)

(($Stage == "MeaninglessQuestion") and ($isPlaying != "True")) => (
    (
        (Stage = "meaninglessQuestion1") or 
        (Stage = "meaninglessQuestion2") or 
        (Stage = "meaninglessQuestion3")
    ) and
    (MeaninglessQuestionStage = "True") and
    (stayActive)
)





(($Event == "FacePreOut") and ($MeaninglessQuestionStage == "True") and ($RecapturingMeaninglessQuestion != "True")) => (
    (ext:Recapture "After:5") and (RecapturingMeaninglessQuestion = "True") and (GPIO 100 0,1,0,0 4000) and (stayActive)
)

(($Event == "FaceOut") and ($MeaninglessQuestionStage == "True")) => (
    (RecapturingMeaninglessQuestion = "False") and (MeaninglessQuestionStage = "False") and (Stage = "Bye") and (stayActive)
)

(($Event == "FaceIn") and ($MeaninglessQuestionStage == "True") and ($RecapturingMeaninglessQuestion == "True")) => (
    (Stage = "MeaninglessQuestion") and (RecapturingMeaninglessQuestion = "False") and (stayActive)
)



(($Stage == "meaninglessQuestion1") and ($isPlaying != "True")) => (
    (say "Это твой первый полет в космос или ты бывалый? ") 
    and (GPIO 100 1,0,1,1 10000)
    and (Stage = "meaninglessResponseWait1") and (stayActive)
)

(($Stage == "meaninglessResponseWait1") and (($ArduinoInput == "0100") or ($ArduinoInput == "0010") or ($ArduinoInput == "0001"))) => (
    (GPIO 100 1,0,1,1 4000)
    and (Stage = "meaninglessResponse1") and (stayActive)
)

(($Stage == "meaninglessResponse1") and ($isPlaying != "True")) => (
    (GPIO 100 0,1,0,0 4000)
    and (say "я за тебя рада ") 
    and (Stage = "preFactQuestion")
    and (MeaninglessQuestionStage = "False") and (stayActive)
)





(($Stage == "meaninglessQuestion2") and ($isPlaying != "True")) => (
    (say "Ненавидишь ли ты блокчейн так же, как и я? ") 
    and (GPIO 100 1,0,1,1 10000)
    and (Stage = "meaninglessResponseWait2") and (stayActive)
)

(($Stage == "meaninglessResponseWait2") and (($ArduinoInput == "0100") or ($Event == "Key_6") )) => (
    (GPIO 100 1,0,1,1 4000)
    and (Stage = "meaninglessResponseYes2") and (stayActive)
)

(($Stage == "meaninglessResponseWait2") and (($ArduinoInput == "0010") or ($ArduinoInput == "0001"))) => (
    (GPIO 100 1,0,1,1 4000)
    and (Stage = "meaninglessResponseNo2") and (stayActive)
)

(($Stage == "meaninglessResponseNo2") and ($isPlaying != "True")) => (
    (GPIO 100 0,1,0,0 4000)
    and (say "Если ты ещё и майнер, развернись и беги, космос не для тебя! ") 
    and (Stage = "preFactQuestion")
    and (MeaninglessQuestionStage = "False") and (stayActive)
)

(($Stage == "meaninglessResponseYes2") and ($isPlaying != "True")) => (
    (GPIO 100 0,1,0,0 4000)
    and (say "Правильно!") 
    and (Stage = "preFactQuestion")
    and (MeaninglessQuestionStage = "False") and (stayActive)
)



(($Stage == "meaninglessQuestion3") and ($isPlaying != "True")) => (
    ((say "C3PO просто красавчик! А ты его тоже любишь скажи? ") or (say "Любишь ли ты Илона Маска так же, как люблю его я?"))
    and (GPIO 100 1,0,1,1 10000)
    and (Stage = "meaninglessResponseWait3") and (stayActive)
)

(($Stage == "meaninglessResponseWait3") and (($ArduinoInput == "0100") or ($ArduinoInput == "0010") or ($ArduinoInput == "0001"))) => (
    (GPIO 100 1,0,1,1 4000)
    and (Stage = "meaninglessResponse3") and (stayActive)
)

(($Stage == "meaninglessResponse3") and ($isPlaying != "True")) => (
    (GPIO 100 0,1,0,0 4000)
    and ((say "Мне он очень по душе") or (say "как же он хорош"))
    and (Stage = "preFactQuestion")
    and (MeaninglessQuestionStage = "False") and (stayActive)
)

(($Stage == "preFactQuestion") and ($isPlaying != "True")) => (
    (say "Хочешь, расскажу что-нибудь занятное про космос?") and 
    (GPIO 100 1,0,1,1 10000) and
    (Stage = "preFactResponseWait")
)

(($Stage == "preFactResponseWait") and (($ArduinoInput == "0100") or ($Event == "Key_6") )) => (
    (GPIO 100 0,1,0,0 4000) and
    (clear $Event) and
    (Stage = "preFactResponse")
)

(($Stage == "preFactResponseWait") and (($ArduinoInput == "0010") or ($Event == "Key_7") )) => (
    (GPIO 100 0,1,0,0 4000) and
    (clear $Event) and
    (Stage = "preQuizQuestion")
)


(($Stage == "preFactResponse") and ($isPlaying != "True")) => (
    (say "{Медведи в России встречаются в 19 раз чаще, чем астероиды в Главном астероидном поясе|Если заплакать в космосе, слезы останутся на глазах и лице.|Газовое облако в созвездии Орел содержит достаточно алкоголя, чтобы создать 200 септиллионов литров пива.|
Если бы вы упали в черную дыру, вас бы растянуло как лапшу. Явление называется спаггетификация.|Если ничто не потревожит Луну например, метеорит, то следы, оставленные на ее поверхности, останутся нетронутыми навсегда. В отличие от Земли тут нет эрозии, вызванной ветром и водой.
Навозные жуки ориентируются по Млечному пути.Птицы, тюлени и люди используют звезды для ориентирования, но африканские навозные жуки используют целую галактику.|Более двухста двенадцати лет вам бы понадобилось, чтобы на обычном автомобиле добраться до Солнца.}") 
    and (Stage = "preQuizQuestion")
)

(($Stage == "preQuizQuestion") and ($isPlaying != "True")) => (
    (GPIO 100 1,0,1,1 20000) and (Stage = "QuizQuest")
)


(($Stage == "Bye") and ($isPlaying != "True")) => (
     (GPIO 100 0,0,0,0 10000) and (say "Сеанс связи окончен") and (Stage = "afterBye")
)
